plugins {
	id 'fabric-loom' version '1.1.+'
	id "io.github.p03w.machete" version "1.+"
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${archives_base_name}-mc${mc_version}-${modloader}"
version = "v${mod_version}"
group = project.maven_group

repositories {
	mavenLocal()
	mavenCentral()

	maven { url = 'https://maven.parchmentmc.org' }						// You guessed it.
	maven { url = "https://api.modrinth.com/maven" }					// LazyDFU, Suggestion Tweaker, Sodium, Iris
	maven { url = "https://maven.terraformersmc.com/releases/" }		// Mod Menu, EMI
	maven { url = "https://maven.shedaniel.me/" }						// REI

	// CC:T
	maven { url = "https://squiddev.cc/maven" }							// Goodbye CC:R...

	// Create : Primary
	maven { url = "https://mvn.devos.one/snapshots/" }					// Create, Porting Lib, Forge Tags, Milk Lib

	// Also Create : Secondary (...why do I have to import this as well?)
	maven { url = "https://maven.tterrag.com/" }						// Registrate and Flywheel
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }	// Reach Entity Attributes
	maven { url = "https://cursemaven.com" }							// Forge Config API Port
	maven { url = "https://jitpack.io" }								// Mixin Extras, fabric ASM
}

dependencies {
	// Setup
	minecraft "com.mojang:minecraft:${project.mc_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${project.mc_version}:${project.parchment_version}@zip")
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// APIs
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}+${mc_version}")
	modApi("com.terraformersmc:modmenu:${modmenu_version}")

	// Create
	modImplementation("com.simibubi.create:create-fabric-${mc_version}:${create_version}+mc${mc_version}")

	// CC:T
	modImplementation("cc.tweaked:cc-tweaked-${mc_version}-core:${project.cc_version}")
	modApi("cc.tweaked:cc-tweaked-${mc_version}-fabric-api:${project.cc_version}")

	//////// Dev environment ////////
	modLocalRuntime("cc.tweaked:cc-tweaked-${mc_version}-fabric:${project.cc_version}")

	modLocalRuntime("maven.modrinth:lazydfu:${lazydfu_version}")
	modLocalRuntime("com.terraformersmc:modmenu:${modmenu_version}")

	// Rendering
	//if (sodium || iris)
	//	modLocalRuntime("maven.modrinth:sodium:${sodium_version}")
	//if (iris)
	//	modLocalRuntime("maven.modrinth:iris:${iris_version}")

	// Recipe viewers: Load REI or EMI
	switch (recipe_viewer.toLowerCase(Locale.ROOT)) {
		case "rei": modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"); break
		case "emi": modLocalRuntime("dev.emi:emi-fabric:${emi_version}"); break
		case "disabled": break
		default: println("Unknown recipe viewer specified: ${recipe_viewer}. Must be REI, EMI, or disabled.")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand(project.properties)
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
	options.compilerArgs <<"-Xlint:deprecation"
}

jar {
	from("LICENSE") {
		rename {
			"${it}_${project.archivesBaseName}"
		}
	}
	manifest {
		attributes([
				"Specification-Title"     : "${project.archives_base_name}",
				"Specification-Vendor"    : "${project.maven_group}",
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : project.mod_version,
				"Implementation-Vendor"   : "${project.maven_group}",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories { }
}
